BACKUP ~TTSQ/Backup~        
AUTHOR ~XXX~ 

VERSION ~v7~

README ~TTSQ/ReadMe-TTSQ.txt~

ALWAYS
 ACTION_DEFINE_ARRAY tra_reload BEGIN setup END
 LAF HANDLE_CHARSETS
 INT_VAR
 infer_charset = 1
 STR_VAR
 tra_path = EVAL ~%MOD_FOLDER%/Languages~
 reload_array = tra_reload
 END
 END

AUTO_TRA ~TTSQ/Languages/%s~

LANGUAGE ~English~ ~english~
         ~TTSQ/Languages/english/setup.tra~
LANGUAGE ~Russian (translation by aerie.ru)~ ~russian~
         ~TTSQ/Languages/russian/setup.tra~
LANGUAGE ~Deutsch (Uebersetzung von Leonardo Watson)~ ~deutsch~
         ~TTSQ/Languages/deutsch/setup.tra~
LANGUAGE ~Spanish (Traducido por Clan REO)~ ~spanish~
         ~TTSQ/Languages/spanish/setup.tra~
LANGUAGE ~French (translation by Fouinto (of the d'Oghmatiques)~ ~french~
         ~TTSQ/Languages/french/setup.tra~
LANGUAGE ~Italian (translation by Ilot)~ ~italian~
         ~TTSQ/Languages/italian/setup.tra~

BEGIN @0

///////////////////////
//Installation items
///////////////////////
COPY ~TTSQ/Itm/Metwaso.itm~          ~override/Metwaso.itm~        
  SAY NAME1 @1 
  SAY NAME2 @2 
  SAY UNIDENTIFIED_DESC @3 
  SAY DESC @4

/////////////////////////
//Installation creatures
/////////////////////////
COPY ~TTSQ/cre/metbae.cre~           ~override/metbae.cre~         
  SAY NAME1 @5 
  SAY NAME2 @5 

COPY ~TTSQ/cre/metbae2.cre~          ~override/metbae2.cre~        
  SAY NAME1 @5 
  SAY NAME2 @5 

/////////////////////////
//Installation areas
/////////////////////////
COPY_EXISTING ~AR0805.are~ ~override~
  READ_LONG 0x54 actors_off
  READ_SHORT 0x58 actors_num
  FOR (i = 0; i < %actors_num%; i += 1) BEGIN
    READ_ASCII (%actors_off% + %i% * 0x110 + 0x80) actor_resref
    PATCH_IF !("%actor_resref%" STRING_COMPARE_CASE "MUMMY01") BEGIN
      WRITE_ASCII (%actors_off% + %i% * 0x110 + 0x80) MUMGRE01 (8)
    END
  END
	FOR (i = 0; i < %actors_num%; i += 1) BEGIN
    READ_ASCII (%actors_off% + %i% * 0x110 + 0x80) actor_resref
    PATCH_IF !("%actor_resref%" STRING_COMPARE_CASE "SKELWA01") BEGIN
      WRITE_ASCII (%actors_off% + %i% * 0x110 + 0x80) VAMANC01 (8)
    END
  END  
BUT_ONLY_IF_IT_CHANGES

ACTION_IF NOT FILE_EXISTS_IN_GAME ~AR0805.bcs~
	THEN BEGIN
		COPY_EXISTING ~AR0805.ARE~ ~override~
			WRITE_ASCII 0x94 ~AR0805~ #8
		COMPILE ~TTSQ/scripts/AR0805.baf~
END
	ELSE BEGIN
		EXTEND_BOTTOM ~AR0805.bcs~ ~TTSQ/scripts/AR0805.baf~
END

/////////////////////////
//Installation scripts
/////////////////////////
EXTEND_BOTTOM ~BALDUR.BCS~ ~TTSQ/scripts/BALDUR.BAF~
COMPILE ~TTSQ/baf~

COMPILE ~TTSQ/d~

////////////////////////////////////
//End of The Tortured Soul Quest
////////////////////////////////////